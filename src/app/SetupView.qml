pragma ComponentBehavior: Bound
import OttqApp
import QtQuick
import QtQuick.Controls.Basic
import QtQuick.Layouts

Item {
    id: suRoot

    property quizConfiguration config

    Column {
        anchors.centerIn: parent
        spacing: 10

        Item {
            height: timesTablesTitle.height
            width: parent.width

            SectionTitle {
                id: timesTablesTitle

                anchors.horizontalCenter: parent.horizontalCenter
                subtitle: {
                    var numbers = suRoot.config.timesTablesStr;
                    return numbers === "" ? qsTr("Add a number") : numbers;
                }
                title: qsTr("Times Tables:")
            }
        }

        Item {
            height: timesTableNumber.height + 10 + btnAddTimesTable.height
            width: parent.width

            SpinBox {
                id: timesTableNumber

                anchors.horizontalCenter: parent.horizontalCenter
                from: 1
                to: 100
                value: timesTableNumberDecimalStep.value

                onValueChanged: {
                    if (suRoot.config.contains(timesTableNumber.value))
                        btnAddTimesTable.state = "remove";
                    else
                        btnAddTimesTable.state = "";
                }
            }

            Slider {
                id: timesTableNumberDecimalStep

                anchors.bottom: btnAddTimesTable.bottom
                anchors.left: timesTableNumber.right
                anchors.leftMargin: 5
                anchors.top: timesTableNumber.top
                from: 90
                height: parent.height
                orientation: Qt.Vertical
                stepSize: 10
                to: 10
            }

            Button {
                id: btnAddTimesTable

                anchors.horizontalCenter: parent.horizontalCenter
                anchors.left: timesTableNumber.left
                anchors.top: timesTableNumber.bottom
                anchors.topMargin: 10
                text: qsTr("Add")
                width: timesTableNumber.width

                states: [
                    State {
                        name: "remove"

                        PropertyChanges {
                            btnAddTimesTable {
                                text: qsTr("Remove")

                                onClicked: {
                                    var n = timesTableNumber.value;
                                    if (suRoot.config.remove(n))
                                        state = "";
                                }
                            }
                        }
                    }
                ]

                onClicked: {
                    if (suRoot.config.addTimesTable(timesTableNumber.value))
                        state = "remove";
                }
            }
        }

        MenuSeparator {
            anchors.horizontalCenter: parent.horizontalCenter
            width: parent.width
        }

        GridLayout {
            anchors.horizontalCenter: parent.horizontalCenter
            columns: 3
            rowSpacing: 2
            rows: 2
            width: parent.width

            IncrementButton {
                firstNumberOfRange: true
            }

            DecrementButton {
                firstNumberOfRange: true
            }

            SectionTitle {
                id: factorRangeTitle

                Layout.alignment: Qt.AlignHCenter | Qt.AlignTop
                Layout.column: 1
                Layout.row: 0
                Layout.rowSpan: 2
                subtitle: {
                    var first = factorRange.intFirst;
                    var second = factorRange.intSecond;
                    return "[%1, %2]".arg(first).arg(second);
                }
                title: qsTr("Factors:")
            }

            IncrementButton {
                firstNumberOfRange: false
            }

            DecrementButton {
                firstNumberOfRange: false
            }
        }

        RangeSlider {
            id: factorRange

            property int intFirst: Math.round(first.value)
            property int intSecond: Math.round(second.value)

            anchors.horizontalCenter: parent.horizontalCenter
            first.value: suRoot.config.factorRange.first
            from: 1
            second.value: suRoot.config.factorRange.second
            snapMode: RangeSlider.SnapAlways
            stepSize: 1
            to: 100

            first.onMoved: suRoot.config.factorRange.first = intFirst
            second.onMoved: suRoot.config.factorRange.second = intSecond
        }
    }

    component DecrementButton: StepButton {
        Layout.row: 1
        text: "-"

        onClicked: {
            if (firstNumberOfRange) {
                factorRange.first.decrease();
                factorRange.first.moved();
            } else {
                factorRange.second.decrease();
                factorRange.second.moved();
            }
        }
    }
    component IncrementButton: StepButton {
        Layout.row: 0
        text: "+"

        onClicked: {
            if (firstNumberOfRange) {
                factorRange.first.increase();
                factorRange.first.moved();
            } else {
                factorRange.second.increase();
                factorRange.second.moved();
            }
        }
    }
    component SectionTitle: Column {
        id: stRoot

        required property string subtitle
        required property string title

        spacing: 2

        Text {
            anchors.horizontalCenter: parent.horizontalCenter
            color: "#888"
            text: stRoot.title
        }

        Text {
            anchors.horizontalCenter: parent.horizontalCenter
            font.bold: true
            text: stRoot.subtitle
        }
    }
    component StepButton: RoundButton {
        // An enum inside an inline component is not usable (Qt 6.10), so it
        // would be defined at the top of the root component, far away from its
        // usage when using qmlformat.
        // Also, the lines become much longer.
        required property bool firstNumberOfRange // first/second of RangeSlider

        Layout.alignment: firstNumberOfRange ? Qt.AlignLeft : Qt.AlignRight
        Layout.column: firstNumberOfRange ? 0 : 2
        Layout.preferredHeight: factorRangeTitle.height / 2
        flat: true
    }
}
